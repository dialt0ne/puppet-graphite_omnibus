# == Class: graphite_omnibus
#
# Configures a graphite_omnibus server
#
# === Authors
#
# Anthony Tonns <antony@tonns.com>
#
class graphite_omnibus (
    $package_ensure             = $::graphite_omnibus::params::package_ensure,

    $relay_service_ensure       = $::graphite_omnibus::params::relay_service_ensure,
    $cache_service_ensure       = $::graphite_omnibus::params::cache_service_ensure,
    $aggregation_service_ensure = $::graphite_omnibus::params::aggregation_service_ensure,
    $web_service_ensure         = $::graphite_omnibus::params::web_service_ensure,

    $relay_sysconfig            = $::graphite_omnibus::params::relay_sysconfig,
    $cache_sysconfig            = $::graphite_omnibus::params::cache_sysconfig,
    $aggregation_sysconfig      = $::graphite_omnibus::params::aggregation_sysconfig,
    $web_sysconfig              = $::graphite_omnibus::params::web_sysconfig,

    $aggregation_rules_conf     = $::graphite_omnibus::params::aggregation_rules_conf,
    $aggregation_rules          = $::graphite_omnibus::params::aggregation_rules,
    $blacklist_conf             = $::graphite_omnibus::params::blacklist_conf,
    $blacklist                  = $::graphite_omnibus::params::blacklist,
    $carbon_conf                = $::graphite_omnibus::params::carbon_conf,
    $carbon                     = $::graphite_omnibus::params::carbon,
    $relay_rules_conf           = $::graphite_omnibus::params::relay_rules_conf,
    $relay_rules                = $::graphite_omnibus::params::relay_rules,
    $rewrite_rules_conf         = $::graphite_omnibus::params::rewrite_rules_conf,
    $rewrite_rules              = $::graphite_omnibus::params::rewrite_rules,
    $storage_schemas_conf       = $::graphite_omnibus::params::storage_schemas_conf,
    $storage_schemas            = $::graphite_omnibus::params::storage_schemas,
    $storage_aggregation_conf   = $::graphite_omnibus::params::storage_aggregation_conf,
    $storage_aggregation        = $::graphite_omnibus::params::storage_aggregation,
    $whitelist_conf             = $::graphite_omnibus::params::whitelist_conf,
    $whitelist                  = $::graphite_omnibus::params::whitelist,

    $dashboard_conf             = $::graphite_omnibus::params::dashboard_conf,
    $graphtemplates_conf        = $::graphite_omnibus::params::graphtemplates_conf,
    $graphite_web_gunicorn_conf = $::graphite_omnibus::params::graphite_web_gunicorn_conf,
    $graphite_web_gunicorn      = $::graphite_omnibus::params::graphite_web_gunicorn,
    $graphite_my_cnf            = $::graphite_omnibus::params::graphite_my_cnf,
    $graphite_mysql             = $::graphite_omnibus::params::graphite_mysql,
    $local_settings_py          = $::graphite_omnibus::params::local_settings_py,
    $local_settings             = $::graphite_omnibus::params::local_settings,
) inherits graphite_omnibus::params {

    class { '::graphite_omnibus::install':
        package_ensure  => $package_insure,
    }

    # the graphite services
    service { 'graphite-carbon-relay': 
        ensure      => $relay_service_ensure, 
        enable      => $relay_service_ensure, 
        hasrestart  => true,
        hasstatus   => true,
        require     => Package['graphite-omnibus'],
    }

    service { 'graphite-carbon-cache': 
        ensure      => $cache_service_ensure, 
        enable      => $cache_service_ensure, 
        hasrestart  => true,
        hasstatus   => true,
        require     => Package['graphite-omnibus'],
    }

    service { 'graphite-carbon-aggregation': 
        ensure      => $aggregation_service_ensure, 
        enable      => $aggregation_service_ensure, 
        hasrestart  => true,
        hasstatus   => true,
        require     => Package['graphite-omnibus'],
    }

    service { 'graphite-web-gunicorn': 
        ensure      => $web_service_ensure, 
        enable      => $web_service_ensure, 
        hasrestart  => true,
        hasstatus   => true,
        require     => Package['graphite-omnibus'],
    }

    # default file permissions
    File {
        owner   => 'root',
        group   => 'root',
        mode    => 0644,
    }

    # graphite-carbon-relay
    file { '/etc/sysconfig/carbon-relay':
        ensure  => 'file',
        content => join(flatten(["# generated by puppet",$relay_sysconfig]),"\n"),
        notify  => Service['graphite-carbon-relay'],
    }
    # graphite-carbon-cache
    file { '/etc/sysconfig/carbon-cache':
        ensure  => 'file',
        content => join(flatten(["# generated by puppet",$cache_sysconfig]),"\n"),
        notify  => Service['graphite-carbon-cache'],
    }
    # graphite-carbon-aggregation
    file { '/etc/sysconfig/carbon-aggregation':
        ensure  => 'file',
        content => join(flatten(["# generated by puppet",$aggregation_sysconfig]),"\n"),
        notify  => Service['graphite-carbon-aggregation'],
    }
    # graphite-web-gunicorn
    file { '/etc/sysconfig/graphite-web':
        ensure  => 'file',
        content => join(flatten(["# generated by puppet",$web_sysconfig]),"\n"),
        notify  => Service['graphite-web-gunicorn'],
    }

    # carbon daemon configs
    file { '/opt/graphite/conf/aggregation-rules.conf':
        ensure  => 'file',
        content => template($aggregation_rules_conf),
        require => File['/opt/graphite/conf'],
    }

    file { '/opt/graphite/conf/blacklist.conf':
        ensure  => 'file',
        content => template($blacklist_conf),
        require => File['/opt/graphite/conf'],
        notify  => [
            Service['graphite-carbon-relay'],
            Service['graphite-carbon-cache'],
            Service['graphite-carbon-aggregation'],
        ],
    }

    file { '/opt/graphite/conf/carbon.conf':
        ensure  => 'file',
        content => template($carbon_conf),
        require => File['/opt/graphite/conf'],
        notify  => [
            Service['graphite-carbon-relay'],
            Service['graphite-carbon-cache'],
            Service['graphite-carbon-aggregation'],
        ],
    }

    file { '/opt/graphite/conf/relay-rules.conf':
        ensure  => 'file',
        content => template($relay_rules_conf),
        require => File['/opt/graphite/conf'],
        notify  => [
            Service['graphite-carbon-relay'],
        ],
    }

    file { '/opt/graphite/conf/rewrite-rules.conf':
        ensure  => 'file',
        content => template($rewrite_rules_conf),
        require => File['/opt/graphite/conf'],
        notify  => [
            Service['graphite-carbon-relay'],
            Service['graphite-carbon-cache'],
            Service['graphite-carbon-aggregation'],
        ],
    }

    file { '/opt/graphite/conf/storage-schemas.conf':
        ensure  => 'file',
        content => template($storage_schemas_conf),
        require => File['/opt/graphite/conf'],
        notify  => [
            Service['graphite-carbon-cache'],
        ],
    }

    file { '/opt/graphite/conf/storage-aggregation.conf':
        ensure  => 'file',
        content => template($storage_aggregation_conf),
        require => File['/opt/graphite/conf'],
        notify  => [
            Service['graphite-carbon-cache'],
        ],
    }

    file { '/opt/graphite/conf/whitelist.conf':
        ensure  => 'file',
        content => template($whitelist_conf),
        require => File['/opt/graphite/conf'],
        notify  => [
            Service['graphite-carbon-relay'],
            Service['graphite-carbon-cache'],
            Service['graphite-carbon-aggregation'],
        ],
    }

    # graphite-web configs
    file { '/opt/graphite/conf/graphite.wsgi':
        ensure  => 'file',
        content => template($graphite_wsgi_conf),
        require => File['/opt/graphite/conf'],
        notify  => [
            Service['graphite-web-gunicorn'],
        ],
    }
    file { '/opt/graphite/conf/graphite_wsgi.py':
        ensure  => 'link',
        target  => 'graphite.wsgi',
        require => File['/opt/graphite/conf/graphite.wsgi'],
        notify  => [
            Service['graphite-web-gunicorn'],
        ],
    }

    file { '/opt/graphite/conf/dashboard.conf':
        ensure  => 'file',
        source  => $dashboard_conf,
        require => File['/opt/graphite/conf'],
        notify  => [
            Service['graphite-web-gunicorn'],
        ],
    }
    file { '/opt/graphite/conf/graphTemplates.conf':
        ensure  => 'file',
        source  => $graphtemplates_conf,
        require => File['/opt/graphite/conf'],
        notify  => [
            Service['graphite-web-gunicorn'],
        ],
    }

    file { '/opt/graphite/conf/graphite-web-gunicorn.conf':
        ensure  => 'file',
        content => template($graphite_web_gunicorn_conf),
        require => File['/opt/graphite/conf'],
        notify  => [
            Service['graphite-web-gunicorn'],
        ],
    }

    file { '/opt/graphite/conf/graphite-my.cnf':
        ensure  => 'file',
        content => template($graphite_my_cnf),
        require => File['/opt/graphite/conf'],
        notify  => [
            Service['graphite-web-gunicorn'],
        ],
    }

    file { '/opt/graphite/webapp/graphite/local_settings.py':
        ensure  => 'file',
        content => template($local_settings_py),
        require => File['/opt/graphite/webapp/graphite'],
        notify  => [
            Service['graphite-web-gunicorn'],
        ],
    }
    file { '/opt/graphite-omnibus/graphite/lib/graphite/local_settings.py':
        ensure  => 'link',
        target  => '/opt/graphite/webapp/graphite/local_settings.py',
        require => File['/opt/graphite/webapp/graphite/local_settings.py'],
        notify  => [
            Service['graphite-web-gunicorn'],
        ],
    }

}
